// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task allConnectedChecksWithGCE {
    group = "customtasks"
    description = "Starts GCE-Backend before executing every connected check. Finally shuts down the GCE-Backend."

    def dependencies = [':gceInBackground']
    getTasksByName('connectedCheck', true).each {
        dependencies.add(it.getPath())
        it.mustRunAfter ':gceInBackground'
    }

    dependsOn dependencies
    finalizedBy ':backend-gce:appengineStop'
}

task gceInBackground {
    doLast {
        // executes the gce-wrapper on a separate thread so it does not
        // block the execution of other tasks
        def myThread = Thread.start {
            gceStartWrapper.execute()
        }

        // sleep for 10 seconds to make sure that the server has been
        // setup before executing further tasks
        sleep 10000
    }
}

task gceStartWrapper(type: Exec) {
    workingDir '.'
    commandLine = ['cmd', '/c', 'gradle' , ':backend-gce:appengineRun']
}